apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {

    compileSdkVersion 31

    defaultConfig {
        applicationId "com.kondenko.pocketwaka"

        minSdkVersion 22
        targetSdkVersion 31

        def appMajor = 2
        def appMinor = 3
        def appPatch = 1
        versionName "${appMajor}.${appMinor}.${appPatch}"
        versionCode 13

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.incremental": "true",]
            }
        }
    }

    signingConfigs {
        config {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("release.properties")))

            storeFile rootProject.file(props.keyStore)
            keyAlias props.keyAlias
            keyPassword props.keyPassword
            storePassword props.storePassword
        }
    }

    buildTypes {
        release {
            def configFiles = fileTree(dir: "./proguard", include: ["*.pro"]).asList()
            configFiles.add(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles configFiles.toArray()
            manifestPlaceholders = [enableCrashReporting: "true"]
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.config
        }
        preRelease {
            initWith release
            debuggable true
        }
        debug {
            shrinkResources false
            minifyEnabled false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        mock {
            initWith debug
            manifestPlaceholders = [enableCrashReporting: "false"]
            buildConfigField "String", "ACCESS_TOKEN", ACCESS_TOKEN
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        File debugKaptFiles = file("$buildDir/generated/source/kapt/debug")

        main {
            ext.enableCrashlytics = false
            java.srcDirs += 'src/main/kotlin'
        }
        release {
            java.srcDirs += [file("$buildDir/generated/source/kapt/release")]
        }
        mock {
            ext.enableCrashlytics = false
            java.srcDirs += 'src/mock/kotlin'
        }
        test {
            ext.enableCrashlytics = false
            java.srcDirs += 'src/test/kotlin'
            java.srcDirs += 'src/testUtils/kotlin'
            java.srcDirs += debugKaptFiles
        }
        androidTest {
            ext.enableCrashlytics = false
            java.srcDirs += 'src/androidTest/kotlin'
            java.srcDirs += debugKaptFiles
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        fatal 'StopShip'
    }
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

kapt {
    generateStubs = false
}

dependencies {

    // kotlinLib
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    // androidKtx
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    // support
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.google.android.material:material:1.3.0-alpha04"
    implementation "androidx.cardview:cardview:${versions.cardview}"
    implementation "androidx.viewpager2:viewpager2:${versions.viewpager2}"
    implementation "androidx.browser:browser:${versions.browser}"

    // archComponents
    implementation "androidx.lifecycle:lifecycle-extensions:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:${versions.lifecycle}"

    // room
    implementation "androidx.room:room-runtime:${versions.room}"
    implementation "androidx.room:room-rxjava2:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    // rx
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxJava}"
    implementation "io.reactivex.rxjava2:rxkotlin:${versions.rxKotlin}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"
    implementation "com.jakewharton.rxbinding3:rxbinding:${versions.rxBinding}"

    // networking
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation('com.squareup.okhttp3:logging-interceptor:4.9.0')

    // smartTabLayout
    implementation "com.ogaclejapan.smarttablayout:library:${versions.smartTabLayout}@aar"
    implementation "com.ogaclejapan.smarttablayout:utils-v4:${versions.smartTabLayout}@aar"

    // koin
    implementation "io.insert-koin:koin-core:${versions.koin}"
    implementation "io.insert-koin:koin-android:${versions.koin}"

    // constraintLayout
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"

    // timber
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    // lottie
    implementation "com.airbnb.android:lottie:${versions.lottie}"

    // firebaseCore
    implementation "com.google.firebase:firebase-core:${versions.firebaseCore}"

    // Analytics
    implementation "com.google.firebase:firebase-analytics:${versions.analytics}"

    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    // remoteConfig
    implementation "com.google.firebase:firebase-config-ktx:20.0.1"

    // calendar
    implementation "com.github.kizitonwose:CalendarView:0.3.4"
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
    debugImplementation "com.amitshekhar.android:debug-db:${versions.databaseBrowser}"

    testImplementation "androidx.test:core:${versions.androidxTest}"
    testImplementation "androidx.test.ext:junit:${versions.junit}"
    testImplementation "androidx.test:runner:${versions.testRunner}"
    testImplementation "androidx.test:rules:${versions.testRunner}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "com.nhaarman:mockito-kotlin:${versions.mockitoKotlin}"
    testImplementation "io.insert-koin:koin-test:${versions.koin}"
    testImplementation "io.insert-koin:koin-test-junit4:${versions.koin}"
    testImplementation("org.robolectric:robolectric:${versions.robolectric}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation('org.threeten:threetenbp:1.5.0') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }

    testImplementation group: 'org.mockito', name: 'mockito-inline', version: versions.mockito

}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"
    }
}