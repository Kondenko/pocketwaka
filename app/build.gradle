apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion versions.androidCompileSdk
    buildToolsVersion versions.androidBuildTools

    defaultConfig {
        applicationId "com.kondenko.pocketwaka"

        minSdkVersion versions.androidMinSdk
        targetSdkVersion versions.androidTargetSdk

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        config {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("release.properties")))

            storeFile rootProject.file(props.keyStore)
            keyAlias props.keyAlias
            keyPassword props.keyPassword
            storePassword props.storePassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.config
            shrinkResources true
            minifyEnabled true
            def configFiles = fileTree(dir: "./proguard", include: ["*.pro"]).asList()
            configFiles.add(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles configFiles.toArray()
        }
        debug {
            shrinkResources false
            minifyEnabled false
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        File debugKaptFiles = file("$buildDir/generated/source/kapt/debug")
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        release {
            java.srcDirs += [file("$buildDir/generated/source/kapt/release")]
        }
        debug {
            java.srcDirs += 'src/debug/kotlin'
            java.srcDirs += debugKaptFiles
        }
        test {
            java.srcDirs += 'src/test/kotlin'
            java.srcDirs += 'src/mock/kotlin'
            java.srcDirs += 'src/testUtils/kotlin'
            java.srcDirs += debugKaptFiles
        }
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
            java.srcDirs += debugKaptFiles
        }
    }

    splits {
        abi {
            exclude "armeabi" // deprecated
        }
    }
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

kapt {
    generateStubs = false
}

dependencies {

    implementation support
    implementation rx
    implementation retrofit
    implementation smartTabLayout
    implementation kotlinLib
    implementation eventBus
    implementation constraintLayout
    implementation dagger
    implementation timber
    kapt daggerCompiler
    kapt databindingCompiler

    testImplementation testingDependencies
    kaptTest testingCompilerDependencies

    androidTestImplementation androidTestingDependencies
    kaptAndroidTest androidTestingCompilerDependencies

    androidTestImplementation(androidTestingSupportDependencies, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
