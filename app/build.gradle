apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.kondenko.pocketwaka"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }


    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        main {
            setRoot('src/main')
            java.srcDirs += 'src/main/kotlin'
            java.srcDirs += [file("$buildDir/generated/source/kapt/debug")]
        }
        test {
            setRoot('src/test')
            java.srcDirs += 'src/test/kotlin'
            java.srcDirs += 'src/mock/kotlin'
            java.srcDirs += 'src/testUtils/kotlin'
            java.srcDirs += [file("$buildDir/generated/source/kapt/debug")]
        }
        androidTest {
            setRoot('src/androidTest')
            java.srcDirs += 'src/androidTest/kotlin'
            java.srcDirs += 'src/mock/kotlin'
            java.srcDirs += 'src/testUtils/kotlin'
            java.srcDirs += [file("$buildDir/generated/source/kapt/debug")]
        }
    }

}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

kapt {
    generateStubs = false
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    kapt "com.android.databinding:compiler:$androidPluginVersion"

    implementation "org.greenrobot:eventbus:$eventBusVersion"

    implementation "com.ogaclejapan.smarttablayout:library:$smartTabLayoutVersion@aar"
    implementation "com.ogaclejapan.smarttablayout:utils-v4:$smartTabLayoutVersion@aar"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // Dagger 2
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    // Support Library
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Testing
    //  Unit testing
    testImplementation "junit:junit:$junitVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"

    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptTest "com.google.dagger:dagger:$daggerVersion"

    // Instrumented testing
    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation "com.android.support.test:runner:$supportTestVersion"
    androidTestImplementation "com.android.support.test:rules:$supportTestVersion"

    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger:$daggerVersion"

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
